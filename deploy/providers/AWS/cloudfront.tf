# Copyright (c) 2026 Guy's and St Thomas' NHS Foundation Trust & King's College London
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

############################################################
# CloudFront hosting for the flip-ui SPA.
#
# Users continue to hit the canonical subdomain
# (stag.flip.aicentre.co.uk / app.flip.aicentre.co.uk); that A-record
# (aws_route53_record.alb in main.tf) aliases this CloudFront distribution.
#
# CloudFront serves the UI static bundle from S3 (default behavior) and
# forwards /api/* to the (internal) ALB via aws_cloudfront_vpc_origin —
# CloudFront provisions an AWS-managed ENI inside our VPC and dials the
# ALB privately. The ALB has no public IP and no internet-facing path:
# its security group accepts ingress on 443 only from the AWS-managed
# CloudFront-VPCOrigins-Service-SG (see aws_security_group_rule.alb_ingress_https_from_cloudfront
# below) — Option 2 from
# https://docs.aws.amazon.com/AmazonCloudFront/latest/DeveloperGuide/private-content-vpc-origins.html.
#
# Origin TLS: CloudFront sets the Host header to var.flip_alb_subdomain
# and the ALB's default listener cert (aws_acm_certificate.flip, see
# main.tf) covers that name, so origin TLS validation passes without
# the legacy api.<subdomain> SNI cert chain.
############################################################

# us-east-1 provider alias is required because CloudFront viewer
# certificates must live in us-east-1 regardless of where the
# distribution serves traffic.
provider "aws" {
  alias  = "us_east_1"
  region = "us-east-1"
}

############################
# CloudFront viewer cert (us-east-1)
############################
#
# Skipped when var.manage_dns is false (the zone-less first LZA bring-up,
# FLIP#749): the viewer cert can't be DNS-validated without the zone, and a
# custom viewer cert is pointless anyway while the distribution has no aliases
# — it serves the default *.cloudfront.net domain with the default cert.

resource "aws_acm_certificate" "flip_cloudfront" {
  count             = var.manage_dns ? 1 : 0
  provider          = aws.us_east_1
  domain_name       = var.flip_alb_subdomain
  validation_method = "DNS"

  lifecycle {
    create_before_destroy = true
  }

  tags = {
    Name = "flip-cloudfront-certificate"
  }
}

resource "aws_route53_record" "cloudfront_cert_validation" {
  for_each = {
    for dvo in var.manage_dns ? tolist(aws_acm_certificate.flip_cloudfront[0].domain_validation_options) : [] : dvo.domain_name => {
      name   = dvo.resource_record_name
      record = dvo.resource_record_value
      type   = dvo.resource_record_type
    }
  }

  allow_overwrite = true
  name            = each.value.name
  records         = [each.value.record]
  ttl             = 60
  type            = each.value.type
  zone_id         = data.aws_route53_zone.subdomain[0].zone_id
}

resource "aws_acm_certificate_validation" "flip_cloudfront" {
  count                   = var.manage_dns ? 1 : 0
  provider                = aws.us_east_1
  certificate_arn         = aws_acm_certificate.flip_cloudfront[0].arn
  validation_record_fqdns = [for record in aws_route53_record.cloudfront_cert_validation : record.fqdn]
}

# State migration for the counts added above (FLIP#749): keeps existing legacy
# states aligned without a manual `terraform state mv`. Safe to remove once
# every live state file has been migrated.
moved {
  from = aws_acm_certificate.flip_cloudfront
  to   = aws_acm_certificate.flip_cloudfront[0]
}

moved {
  from = aws_acm_certificate_validation.flip_cloudfront
  to   = aws_acm_certificate_validation.flip_cloudfront[0]
}

############################
# CloudFront VPC origin for the (internal) ALB
#
# CloudFront provisions an AWS-managed ENI in this VPC and forwards
# /api/* requests to the ALB over HTTPS:443 privately. The ALB has no
# public IP; its security group accepts ingress from the AWS-managed
# CloudFront-VPCOrigins-Service-SG only (see the data source and
# aws_security_group_rule below).
############################

resource "aws_cloudfront_vpc_origin" "flip_api" {
  vpc_origin_endpoint_config {
    # CloudFront VPC origin names accept only alphanumerics, dashes, and
    # underscores — the subdomain contains dots, so replace them with dashes.
    name = "flip-api-vpc-origin-${replace(var.flip_alb_subdomain, ".", "-")}"
    arn  = module.alb.arn
    # Without a hosted zone the ALB cannot carry an ISSUED cert, so the private
    # VPC-origin leg falls back to plain HTTP until DNS lands (FLIP#749; see the
    # ALB listeners comment in main.tf). The ALB's main listener then serves
    # plain HTTP on ALB_HTTPS_PORT, so the HTTP port follows it there. Viewer
    # traffic stays HTTPS either way.
    http_port              = var.manage_dns ? 80 : var.ALB_HTTPS_PORT
    https_port             = 443
    origin_protocol_policy = var.manage_dns ? "https-only" : "http-only"

    origin_ssl_protocols {
      items    = ["TLSv1.2"]
      quantity = 1
    }
  }
}

# CloudFront creates `CloudFront-VPCOrigins-Service-SG` automatically when the
# first VPC origin is provisioned in this VPC. The data lookup depends on the
# VPC origin so a fresh apply doesn't try to read the SG before it exists.
#
# Per AWS docs (Option 2 in
# https://docs.aws.amazon.com/AmazonCloudFront/latest/DeveloperGuide/private-content-vpc-origins.html),
# scoping the ALB ingress to this SG is the documented and most-restrictive
# pattern — it accepts traffic only from this account's CloudFront VPC origin
# ENIs. A vpc_cidr-based rule does NOT work for VPC-origin traffic even
# though the ENIs have IPs inside the VPC CIDR; AWS evaluates VPC-origin SG
# checks against the service-managed SG (or the CloudFront managed prefix
# list), not the ENI source IP.
data "aws_security_group" "cloudfront_vpcorigins_service" {
  name   = "CloudFront-VPCOrigins-Service-SG"
  vpc_id = local.vpc_id

  depends_on = [aws_cloudfront_vpc_origin.flip_api]
}

# Adding this rule via a standalone resource (rather than inside the
# alb_security_group module) avoids a dependency cycle: the ALB needs an SG to
# exist before it can be created, the VPC origin needs the ALB, and the SG
# lookup needs the VPC origin. Attaching the rule outside the module keeps the
# chain linear.
resource "aws_security_group_rule" "alb_ingress_https_from_cloudfront" {
  description              = "HTTPS from the CloudFront-VPCOrigins-Service-SG (Option 2 in AWS VPC origins docs)"
  type                     = "ingress"
  from_port                = var.ALB_HTTPS_PORT
  to_port                  = var.ALB_HTTPS_PORT
  protocol                 = "tcp"
  security_group_id        = module.alb_security_group.security_group.id
  source_security_group_id = data.aws_security_group.cloudfront_vpcorigins_service.id
}

############################
# S3 bucket for CloudFront standard access logs
#
# Used to diagnose CF→ALB origin issues (e.g. the HTTPS-origin 502 that forced
# the /api/* origin to temporarily fall back to HTTP:8080). The log record
# includes x-edge-result-type and x-edge-detailed-result-type which identify
# the exact cause of 5xx responses generated by CloudFront edges.
############################

data "aws_canonical_user_id" "current" {}

resource "aws_s3_bucket" "cloudfront_logs" {
  bucket = "flip-cf-logs-${var.flip_alb_subdomain}"

  tags = {
    Name = "flip-cloudfront-logs"
  }
}

resource "aws_s3_bucket_ownership_controls" "cloudfront_logs" {
  bucket = aws_s3_bucket.cloudfront_logs.id
  rule {
    object_ownership = "BucketOwnerPreferred"
  }
}

resource "aws_s3_bucket_acl" "cloudfront_logs" {
  depends_on = [aws_s3_bucket_ownership_controls.cloudfront_logs]
  bucket     = aws_s3_bucket.cloudfront_logs.id

  access_control_policy {
    owner {
      id = data.aws_canonical_user_id.current.id
    }

    grant {
      grantee {
        type = "CanonicalUser"
        id   = data.aws_canonical_user_id.current.id
      }
      permission = "FULL_CONTROL"
    }

    # awslogsdelivery canonical ID — required for CloudFront standard log delivery.
    # See https://docs.aws.amazon.com/AmazonCloudFront/latest/DeveloperGuide/AccessLogs.html
    grant {
      grantee {
        type = "CanonicalUser"
        id   = "c4c1ede66af53448b93c283ce9448c4ba468c9432aa01d700d3878632f77d2d0"
      }
      permission = "FULL_CONTROL"
    }
  }
}

resource "aws_s3_bucket_lifecycle_configuration" "cloudfront_logs" {
  bucket = aws_s3_bucket.cloudfront_logs.id

  rule {
    id     = "expire-cf-logs-after-30-days"
    status = "Enabled"

    filter {}

    expiration {
      days = 30
    }
  }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "cloudfront_logs" {
  bucket = aws_s3_bucket.cloudfront_logs.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

# CloudFront log delivery uses either canonical-user ACL grants (legacy —
# what this bucket uses today) or an AWS service-principal bucket policy.
# Neither is "public" under PAB semantics, so blocking public ACLs and
# policies is safe regardless of which delivery mechanism is in use.
resource "aws_s3_bucket_public_access_block" "cloudfront_logs" {
  bucket                  = aws_s3_bucket.cloudfront_logs.id
  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

############################
# S3 bucket for UI assets
############################

resource "aws_s3_bucket" "flip_ui" {
  bucket = var.FLIP_UI_BUCKET_NAME

  lifecycle {
    prevent_destroy = true
  }

  tags = {
    Name = "flip-ui"
  }
}

resource "aws_s3_bucket_public_access_block" "flip_ui" {
  bucket                  = aws_s3_bucket.flip_ui.id
  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

resource "aws_s3_bucket_server_side_encryption_configuration" "flip_ui" {
  bucket = aws_s3_bucket.flip_ui.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

resource "aws_s3_bucket_versioning" "flip_ui" {
  bucket = aws_s3_bucket.flip_ui.id

  versioning_configuration {
    status = "Enabled"
  }
}

############################
# CloudFront: OAC, distribution, bucket policy
############################

resource "aws_cloudfront_origin_access_control" "flip_ui" {
  name                              = "flip-ui-${var.flip_alb_subdomain}"
  description                       = "OAC for the flip-ui S3 bucket"
  origin_access_control_origin_type = "s3"
  signing_behavior                  = "always"
  signing_protocol                  = "sigv4"
}

# SPA deep-link rewriter. Attached to the default (S3) cache behavior
# only, so /api/* responses keep their real ALB status codes.
# IMPORTANT: do not add distribution-level custom_error_response blocks
# for 403/404 — they are inherited by /api/* and would mask real error
# codes as 200 index.html (the bug this function exists to avoid).
resource "aws_cloudfront_function" "spa_rewrite" {
  # CloudFront Function names are [A-Za-z0-9-_]{1,64}; strip any other
  # character the subdomain might ever contain.
  name    = "flip-ui-spa-rewrite-${replace(var.flip_alb_subdomain, "/[^a-zA-Z0-9]/", "-")}"
  runtime = "cloudfront-js-2.0"
  comment = "Rewrite SPA deep links (non-asset) to /index.html"
  publish = true
  code    = <<-EOT
    function handler(event) {
      var uri = event.request.uri;
      // Known asset extensions pass through so missing assets 404
      // naturally. Using an allowlist (vs. "any path containing a dot")
      // avoids misrouting dotted SPA segments like /v1.2/projects/42.
      if (/\.(js|css|map|json|html|txt|png|jpe?g|gif|svg|ico|webp|avif|woff2?|ttf|eot)$/i.test(uri)) {
        return event.request;
      }
      event.request.uri = '/index.html';
      return event.request;
    }
  EOT
}

# Managed CloudFront policies (stable AWS-wide IDs; no region dependency).
# See https://docs.aws.amazon.com/AmazonCloudFront/latest/DeveloperGuide/using-managed-cache-policies.html
locals {
  cloudfront_policy_caching_optimized = "658327ea-f89d-4fab-a63d-7e88639e58f6"
  cloudfront_policy_caching_disabled  = "4135ea2d-6df8-44a3-9df3-4b5a84be39ad"

  # Browser-facing origin of the UI, consumed by the S3 bucket CORS rules and
  # the Cognito URLs in services.tf. With DNS managed it is the canonical
  # subdomain (legacy shape, unchanged); on the zone-less first bring-up
  # (FLIP#749) it is the CloudFront default domain, so uploads/downloads and
  # sign-in keep working before any DNS exists. No dependency cycle: the
  # distribution references neither the app buckets' CORS nor Cognito.
  ui_origin = var.manage_dns ? "https://${var.flip_alb_subdomain}" : "https://${aws_cloudfront_distribution.flip_ui.domain_name}"
}

# Custom origin-request policy for /api/*. The managed AllViewer policy
# forwards everything (all headers, cookies, query strings); this narrows
# to the minimum the API actually needs.
#
# Forwarded explicitly:
# - Content-Type: for JSON/multipart requests.
# - Origin: for CORS preflight.
#
# NOT in the whitelist (because CloudFront forwards it automatically and
# the CreateOriginRequestPolicy API rejects it as a reserved parameter):
# - Authorization: always forwarded to custom origins. Carries both
#   Cognito access tokens (user → API) and trust API keys
#   (TRUST_API_KEY_HEADER=Authorization).
#
# Deliberately excluded:
# - X-Internal-Service-Key: fl-server → flip-api traffic is
#   docker-network-internal on the Central Hub and never crosses
#   CloudFront.
#
# Cookies are dropped (API is JWT/stateless). Query strings pass through
# untouched because endpoints use them for filters/pagination and there's
# no central allowlist to vet against.
resource "aws_cloudfront_origin_request_policy" "flip_api" {
  name    = "flip-api-origin-request-${replace(var.flip_alb_subdomain, "/[^a-zA-Z0-9]/", "-")}"
  comment = "Least-privilege origin-request policy for /api/* on ${var.flip_alb_subdomain}"

  headers_config {
    header_behavior = "whitelist"
    headers {
      # Authorization carries the Cognito JWT — without it on the whitelist
      # CloudFront drops the header at the edge and every /api/* request
      # hits flip-api looking like an unauthenticated caller, so the
      # backend's HTTPBearer dependency returns 401 "Not authenticated".
      items = ["Authorization", "Content-Type", "Origin"]
    }
  }

  cookies_config {
    cookie_behavior = "none"
  }

  query_strings_config {
    query_string_behavior = "all"
  }
}

############################
# WAFv2 Web ACL (CloudFront-scoped, us-east-1)
############################

# CloudFront-scoped WAFs must live in us-east-1 regardless of where the
# distribution serves traffic, same constraint as the viewer cert.
#
# Every rule starts in count (shadow) mode so we observe what each rule
# would block before accidentally paging ourselves. The sampled-request
# console (and the CloudWatch log group below) show which requests each
# rule matches; once a rule has been in count mode across a release cycle
# with no false positives, flip its `override_action` (for managed groups)
# or `action` (for the custom rate-limit) to `block` / `none` + `block`.
resource "aws_wafv2_web_acl" "flip_ui_cloudfront" {
  provider = aws.us_east_1
  name     = "flip-ui-${replace(var.flip_alb_subdomain, "/[^a-zA-Z0-9]/", "-")}"
  scope    = "CLOUDFRONT"

  default_action {
    allow {}
  }

  rule {
    name     = "AWSManagedRulesCommonRuleSet"
    priority = 10

    override_action {
      count {}
    }

    statement {
      managed_rule_group_statement {
        name        = "AWSManagedRulesCommonRuleSet"
        vendor_name = "AWS"
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "flipUiCommonRuleSet"
      sampled_requests_enabled   = true
    }
  }

  rule {
    name     = "AWSManagedRulesKnownBadInputsRuleSet"
    priority = 20

    override_action {
      count {}
    }

    statement {
      managed_rule_group_statement {
        name        = "AWSManagedRulesKnownBadInputsRuleSet"
        vendor_name = "AWS"
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "flipUiKnownBadInputs"
      sampled_requests_enabled   = true
    }
  }

  rule {
    name     = "AWSManagedRulesAmazonIpReputationList"
    priority = 30

    override_action {
      count {}
    }

    statement {
      managed_rule_group_statement {
        name        = "AWSManagedRulesAmazonIpReputationList"
        vendor_name = "AWS"
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "flipUiIpReputation"
      sampled_requests_enabled   = true
    }
  }

  rule {
    name     = "RateLimitPerIp"
    priority = 100

    # Starts in count mode; flip to `block {}` once sampled traffic confirms
    # no legitimate client (including trust pollers) trips the threshold.
    action {
      count {}
    }

    statement {
      rate_based_statement {
        limit              = 2000
        aggregate_key_type = "IP"
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "flipUiRateLimit"
      sampled_requests_enabled   = true
    }
  }

  visibility_config {
    cloudwatch_metrics_enabled = true
    metric_name                = "flipUiWebAcl"
    sampled_requests_enabled   = true
  }

  tags = {
    Name = "flip-ui-waf-${var.flip_alb_subdomain}"
  }
}

# WAF logging destination. Name MUST start with `aws-waf-logs-` per AWS —
# otherwise PutLoggingConfiguration rejects it.
resource "aws_cloudwatch_log_group" "flip_ui_waf" {
  provider          = aws.us_east_1
  name              = "aws-waf-logs-flip-ui-${replace(var.flip_alb_subdomain, "/[^a-zA-Z0-9]/", "-")}"
  retention_in_days = 30
}

resource "aws_wafv2_web_acl_logging_configuration" "flip_ui_cloudfront" {
  provider                = aws.us_east_1
  resource_arn            = aws_wafv2_web_acl.flip_ui_cloudfront.arn
  log_destination_configs = [aws_cloudwatch_log_group.flip_ui_waf.arn]
}

############################
# Response headers policy (static / SPA behavior only)
############################

# Attached to the default (S3) cache behavior only. Do NOT attach to /api/*
# — per the viewer-request-function caveat above, distribution-level or
# api-behavior-level headers can leak into API responses and break API
# consumers that do not expect them.
#
# HSTS omits `preload` deliberately: joining the HSTS preload list is a
# one-way door across every subdomain of the apex and belongs in its own
# coordinated PR, not here.
#
# CSP ships in report-only initially so legitimate violations surface in
# browser console + `Content-Security-Policy-Report-Only` response headers
# without blocking traffic. After one release cycle with no real-user
# violations, move the policy body from `content_security_policy_report_only`
# to `content_security_policy` (enforcing). Tracked in
# https://github.com/londonaicentre/FLIP/issues/417.
resource "aws_cloudfront_response_headers_policy" "flip_ui_spa" {
  name    = "flip-ui-spa-${replace(var.flip_alb_subdomain, "/[^a-zA-Z0-9]/", "-")}"
  comment = "Security response headers for the flip-ui SPA at ${var.flip_alb_subdomain}"

  security_headers_config {
    strict_transport_security {
      access_control_max_age_sec = 31536000
      include_subdomains         = true
      preload                    = false
      override                   = true
    }

    content_type_options {
      override = true
    }

    referrer_policy {
      referrer_policy = "strict-origin-when-cross-origin"
      override        = true
    }

    frame_options {
      frame_option = "DENY"
      override     = true
    }
    # `security_headers_config.content_security_policy` is enforce-only; we
    # ship CSP as `Content-Security-Policy-Report-Only` below in
    # custom_headers_config until a release cycle confirms no false
    # positives, then move the body here and delete the custom header.
    #
    # Stage 1 of GHSA-vp94-g35p-29w8: unsafe-inline removed from style-src
    # while staying in report-only. Stage 2 (enforcing) tracked in
    # https://github.com/londonaicentre/FLIP/issues/417.
  }

  custom_headers_config {
    items {
      header = "Content-Security-Policy-Report-Only"
      value = join(" ", [
        "default-src 'self';",
        # CSP source expressions only allow wildcards in the leftmost
        # position (`*.amazonaws.com` OK, `cognito-idp.*.amazonaws.com`
        # is not — the browser silently drops invalid entries). Pin to
        # the deployed region so we keep the allowlist tight; update if
        # the pool is moved to a different region.
        "connect-src 'self' https://cognito-idp.eu-west-2.amazonaws.com https://cognito-identity.eu-west-2.amazonaws.com;",
        "img-src 'self' data:;",
        # unsafe-inline removed from style-src per GHSA-vp94-g35p-29w8:
        # Vue+TailwindCSS generated CSS is bundled as static files, not
        # inline styles. Dynamic style bindings were found to be replaceable
        # with utility classes. If new violations surface during the
        # report-only cycle, audit and fix at the source rather than relaxing.
        "style-src 'self';",
        "script-src 'self';",
        "object-src 'none';",
        "frame-ancestors 'none';",
      ])
      override = true
    }
  }
}

############################
# Response headers policy (/api/* responses)
############################

# Security headers for /api/* responses. Only includes headers that are
# safe and beneficial for API JSON responses. CSP is intentionally
# excluded — it belongs on the SPA (HTML) only, and applying it here
# would leak CSP policy into every API response for no security benefit.
resource "aws_cloudfront_response_headers_policy" "flip_api" {
  name    = "flip-api-${replace(var.flip_alb_subdomain, "/[^a-zA-Z0-9]/", "-")}"
  comment = "Security response headers for /api/* at ${var.flip_alb_subdomain}"

  security_headers_config {
    strict_transport_security {
      access_control_max_age_sec = 31536000
      include_subdomains         = true
      preload                    = false
      override                   = true
    }

    content_type_options {
      override = true
    }

    referrer_policy {
      referrer_policy = "strict-origin-when-cross-origin"
      override        = true
    }

    frame_options {
      frame_option = "DENY"
      override     = true
    }
  }
}

resource "aws_cloudfront_distribution" "flip_ui" {
  enabled             = true
  is_ipv6_enabled     = true
  http_version        = "http2"
  default_root_object = "index.html"
  price_class         = "PriceClass_100"
  # No aliases without DNS (FLIP#749): CloudFront only allows the default
  # viewer cert when no aliases are set, and an alias without a record pointing
  # at it is unreachable anyway. The distribution serves *.cloudfront.net until
  # MANAGE_DNS flips to true.
  aliases    = var.manage_dns ? [var.flip_alb_subdomain] : []
  comment    = "flip-ui at ${var.flip_alb_subdomain}"
  web_acl_id = aws_wafv2_web_acl.flip_ui_cloudfront.arn

  origin {
    domain_name              = aws_s3_bucket.flip_ui.bucket_regional_domain_name
    origin_id                = "s3-flip-ui"
    origin_access_control_id = aws_cloudfront_origin_access_control.flip_ui.id
  }

  origin {
    # domain_name drives the Host header CloudFront sends to the origin and
    # the SNI hostname it presents for TLS validation. Routing is via the
    # VPC origin ID, not DNS, so this value need not resolve from CloudFront
    # itself. The ALB listener cert (aws_acm_certificate.flip) is issued for
    # var.flip_alb_subdomain, so origin TLS validation passes.
    domain_name = var.flip_alb_subdomain
    origin_id   = "alb-api-origin"

    vpc_origin_config {
      vpc_origin_id = aws_cloudfront_vpc_origin.flip_api.id
    }
  }

  default_cache_behavior {
    target_origin_id           = "s3-flip-ui"
    viewer_protocol_policy     = "redirect-to-https"
    allowed_methods            = ["GET", "HEAD"]
    cached_methods             = ["GET", "HEAD"]
    compress                   = true
    cache_policy_id            = local.cloudfront_policy_caching_optimized
    response_headers_policy_id = aws_cloudfront_response_headers_policy.flip_ui_spa.id

    # Attached to the S3 default behavior only — /api/* has its own
    # cache behavior and never invokes this function.
    function_association {
      event_type   = "viewer-request"
      function_arn = aws_cloudfront_function.spa_rewrite.arn
    }
  }

  ordered_cache_behavior {
    path_pattern               = "/api/*"
    target_origin_id           = "alb-api-origin"
    viewer_protocol_policy     = "https-only"
    allowed_methods            = ["DELETE", "GET", "HEAD", "OPTIONS", "PATCH", "POST", "PUT"]
    cached_methods             = ["GET", "HEAD"]
    compress                   = true
    cache_policy_id            = local.cloudfront_policy_caching_disabled
    origin_request_policy_id   = aws_cloudfront_origin_request_policy.flip_api.id
    response_headers_policy_id = aws_cloudfront_response_headers_policy.flip_api.id
  }

  restrictions {
    geo_restriction {
      restriction_type = "none"
    }
  }

  logging_config {
    bucket          = aws_s3_bucket.cloudfront_logs.bucket_domain_name
    include_cookies = false
    prefix          = "standard-logs/"
  }

  viewer_certificate {
    acm_certificate_arn            = var.manage_dns ? aws_acm_certificate_validation.flip_cloudfront[0].certificate_arn : null
    cloudfront_default_certificate = var.manage_dns ? null : true
    ssl_support_method             = var.manage_dns ? "sni-only" : null
    # AWS forces TLSv1 while the default *.cloudfront.net certificate is in
    # use; pinning TLSv1.2_2021 there would just plan perpetual drift.
    minimum_protocol_version = var.manage_dns ? "TLSv1.2_2021" : "TLSv1"
  }

  tags = {
    Name = "flip-ui-${var.flip_alb_subdomain}"
  }
}

resource "aws_s3_bucket_policy" "flip_ui" {
  bucket = aws_s3_bucket.flip_ui.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Sid       = "AllowCloudFrontOAC"
      Effect    = "Allow"
      Principal = { Service = "cloudfront.amazonaws.com" }
      Action    = "s3:GetObject"
      Resource  = "${aws_s3_bucket.flip_ui.arn}/*"
      Condition = {
        StringEquals = {
          "AWS:SourceArn" = aws_cloudfront_distribution.flip_ui.arn
        }
      }
    }]
  })
}

############################
# Outputs
############################

output "CloudfrontDistributionId" {
  description = "CloudFront distribution ID for flip-ui (used by make deploy-ui for cache invalidation)"
  value       = aws_cloudfront_distribution.flip_ui.id
}

output "CloudfrontDistributionDomain" {
  description = "CloudFront distribution CloudFront-assigned domain (*.cloudfront.net). Use for pre-cutover smoke tests."
  value       = aws_cloudfront_distribution.flip_ui.domain_name
}

output "FlipUiBucketName" {
  description = "S3 bucket holding the UI static assets"
  value       = aws_s3_bucket.flip_ui.bucket
}
